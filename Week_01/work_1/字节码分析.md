## Hello.class文件字节码分析
```
题目说明: 自己写一个简单的Hello.java，里面需要涉及基本类型，四则运行，if和for，然后自己分析一下对应的字节码，有问题群里讨论。
```
$> javap -v Hello.class
```
......
class Hello
  minor version: 0
  major version: 52   ->  java8的major version为52 
  flags: ACC_SUPER    
Constant pool:
   方法符号引用 -> new Object()
   #1 = Methodref          #8.#18         // java/lang/Object."<init>":()V
   字段符号引用 -> System.out
   #2 = Fieldref           #19.#20        // java/lang/System.out:Ljava/io/PrintStream;
   # 字符串字面量.
   #3 = String             #21            // a==b
   ......
   # 类符号引用
   #7 = Class              #26            // Hello
   # utf8->相当于一个记录作用的工具
  #11 = Utf8               Code   ->  方法的代码属性
  #12 = Utf8               LineNumberTable  ->  方法的行号信息
  #15 = Utf8               StackMapTable  ->  
  #33 = Utf8               (Ljava/lang/String;)V -> 方法描述符
  ......
{
  Hello();  构造方法
    descriptor: ()V
    flags:
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1       this.super();           // Method java/lang/Object."<init>":()V  
         4: return
      LineNumberTable: #字节码与java代码文件的位置对应
        line 1: 0

  public static void main(java.lang.String[]);  main方法
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=4, args_size=1
         // a=1
         0: iconst_1
         1: istore_1
         // b=2
         2: iconst_2
         3: istore_2
         // a=a+b
         4: iload_1
         5: iload_2
         6: iadd
         7: istore_1
         // a=a-b
         8: iload_1
         9: iload_2
        10: isub
        11: istore_1
        // a=a*b
        12: iload_1
        13: iload_2
        14: imul
        15: istore_1
        // a=a/b
        16: iload_1
        17: iload_2
        18: idiv
        19: istore_1
        // if(a!=b) goto 36
        20: iload_1
        21: iload_2
        // if(a==b) System.out.println("a==b")
        22: if_icmpne     36
        25: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        28: ldc           #3                  // String a==b
        30: invokevirtual #4                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        33: goto          44
        // System.out.println("a!=b")
        36: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        39: ldc           #5                  // String a!=b
        41: invokevirtual #4                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        // int i = 1;
        44: iconst_1
        45: istore_3
        46: iload_3
        47: iconst_5
        // if(i>5) -> goto 64
        48: if_icmpgt     64
        // System.out.println(i);
        51: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        54: iload_3
        55: invokevirtual #6                  // Method java/io/PrintStream.println:(I)V
        // i++
        58: iinc          3, 1
        // goto for
        61: goto          46
        64: return
      LineNumberTable:
        line 3: 0
        .......
}
SourceFile: "Hello.java"
```
